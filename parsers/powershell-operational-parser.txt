SecurityEvent
| where Channel == "Microsoft-Windows-PowerShell/Operational"
| extend EventData = parse_xml(EventData).EventData.Data
| mv-expand bagexpansion=array EventData
| evaluate bag_unpack(EventData)
| extend Key = tostring(column_ifexists('@Name', "")), Value = tostring(column_ifexists('#text', ""))
| evaluate pivot(Key, take_any(Value), TimeGenerated, Channel, Computer, EventID, MG, ManagementGroupName, _ResourceId)
| project TimeGenerated, Computer, EventID, User = column_ifexists("UserData", ""), Payload = column_ifexists("Payload", ""), MessageNumber = column_ifexists("MessageNumber", ""), MessageTotal = column_ifexists("MessageTotal", ""), Path = column_ifexists("Path", ""), ScriptBlockId = column_ifexists("ScriptBlockId", ""), ContextInfo = column_ifexists("ContextInfo", ""), CommandName = column_ifexists("CommandName", ""), CommandPath = column_ifexists("CommandPath", ""), CommandType = column_ifexists("CommandType", ""), ConnectedUser = column_ifexists("ConnectedUser", ""), Details = column_ifexists("Details", ""), EngineVersion = column_ifexists("EngineVersion", ""), Hash = column_ifexists("Hash", ""), Hashes = column_ifexists("Hashes", ""), HostApplication = column_ifexists("HostApplication", ""), HostID = column_ifexists("HostID", ""), HostName = column_ifexists("HostName", ""), HostVersion = column_ifexists("HostVersion", ""), Lines = column_ifexists("Lines", ""), PipelineID = column_ifexists("PipelineID", ""), PreviousCreationUtcTime = column_ifexists("PreviousCreationUtcTime", ""), RuleName = column_ifexists("RuleName", ""), RunspaceID = column_ifexists("RunspaceID", ""), ScriptBlockID = column_ifexists("ScriptBlockID", ""), ScriptBlockText = column_ifexists("ScriptBlockText", ""), ScriptName = column_ifexists("ScriptName", ""), SequenceNumber = column_ifexists("SequenceNumber", ""), Severity = column_ifexists("Severity", ""), ShellID = column_ifexists("ShellID", ""), Source = column_ifexists("Source", "")
